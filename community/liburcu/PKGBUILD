# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Babeux <christian.babeux@0x80.ca>
# Contributor: Yggdrasil <tetzank at web dot de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - aarch64 support

pkgname=liburcu
pkgver=0.8.7
pkgrel=1
pkgdesc="LGPLv2.1 userspace RCU (read-copy-update) library"
arch=('i686' 'x86_64')
url="http://lttng.org/urcu"
license=('LGPL2.1')
source=(http://lttng.org/files/urcu/userspace-rcu-${pkgver}.tar.bz2)
depends=('glibc')
options=('!libtool')
sha1sums=('5306999b8a3296f3dea91246d92e0a993d732898')

prepare() {
    cd ${srcdir}/userspace-rcu-${pkgver}
    sed -i '/\[arm/i [aarch64], [ARCHTYPE="gcc"],' configure.ac
    autoreconf -fi
}

build() {
    cd ${srcdir}/userspace-rcu-${pkgver}
    ./configure --prefix=/usr
    make
}

package() {
    cd ${srcdir}/userspace-rcu-${pkgver}
    make install DESTDIR=${pkgdir}
}
