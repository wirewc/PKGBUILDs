# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Exynos and RPi support

pkgname=libcec
pkgver=2.2.0
pkgrel=1
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter"
arch=('i686' 'x86_64')
url="http://libcec.pulse-eight.com/"
license=('GPL')
depends=('udev' 'lockdev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Pulse-Eight/$pkgname/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('fd4f47a18d6e0f4b9e6f5831280207ee2b2a5fc2741ae32ae09ad12a8aa52917')

if [[ $CARCH = 'armv6h' ]]; then
  depends+=('raspberrypi-firmware')
  replaces=('libcec-rpi')
  provides=('libcec-rpi')
fi

build() {
  cd "$pkgname-$pkgname-$pkgver"
  autoreconf -vif

  if [[ $CARCH == 'armv7h' ]]; then
    CONFIG='--enable-exynos'
  elif [[ $CARCH == 'armv6h' ]]; then
    CONFIG='--disable-exynos --enable-rpi --with-rpi-include-path=/opt/vc/include --with-rpi-lib-path=/opt/vc/lib'
    unset LDFLAGS
  elif [[ $CARCH == 'arm' ]]; then
    CONFIG='--disable-exynos'
  fi

  ./configure --prefix=/usr $CONFIG
  make
}

package() {
  cd "$pkgname-$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
