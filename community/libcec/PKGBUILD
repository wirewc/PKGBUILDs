# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>
# vim: ft=sh:

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Exynos support on v7
#  - patch to remove default to zero forces

buildarch=14

pkgname=libcec
pkgver=3.1.0
pkgrel=2
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter"
arch=('i686' 'x86_64')
url="http://libcec.pulse-eight.com/"
license=('GPL')
makedepends=('cmake')
depends=('udev' 'lockdev' 'p8-platform' 'libxrandr')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Pulse-Eight/$pkgname/archive/$pkgname-$pkgver.tar.gz"
        '0001-remove-default-to-zero.patch')
sha256sums=('09109d21a1b03f42c9e341d12600f2e4c41038d640269fa75408e2d36126f921'
            '33ce3121e393d7f5c162487d82aac9372a246cdb6eca73f0cfbde61bfaffa50f')

prepare() {
    cd "$pkgname-$pkgname-$pkgver"
    patch -p1 -i ../0001-remove-default-to-zero.patch
}

build() {
    cd "$pkgname-$pkgname-$pkgver"

    if [[ $CARCH == 'armv7h' ]]; then
      CONFIG='-DHAVE_EXYNOS_API=1'
    fi

    mkdir build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBDIR_NOARCH=/usr/lib \
        ${CONFIG}
    make
}

package() {
    cd "$pkgname-$pkgname-$pkgver/build"
    make DESTDIR="$pkgdir" install
}
