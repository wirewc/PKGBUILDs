# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

# ALARM: jdoe0
# --enable-aligned flag is required to work correctly on armv5

pkgname=libtorrent
pkgver=0.13.5
pkgrel=1
pkgdesc='BitTorrent library with a focus on high performance and good code'
url='http://rakshasa.github.io/rtorrent/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rakshasa/${pkgname}/archive/${pkgver}.tar.gz"
        $pkgname-version.patch)
sha1sums=('0c83e8b9ece2c07986632dcc800f893451b23d71'
          'ea80eb35cf5812840315726e67a5243ea6dccad4')

options=('!libtool')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed '/AM_PATH_CPPUNIT/d' -i configure.ac
#https://github.com/rakshasa/libtorrent/issues/82
    patch -Np0 -i "${srcdir}"/$pkgname-version.patch
}


build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./autogen.sh

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"

	[[ $CARCH == 'arm' ]] && CONFIG='--enable-aligned'

	./configure \
		--prefix=/usr \
		--disable-debug $CONFIG

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
