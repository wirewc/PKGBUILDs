# U-Boot: BeagleBoard and BeagleBoard-xM
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-beagleboard
pkgver=2015.04
pkgrel=1
pkgdesc="U-Boot for BeagleBoard and BeagleBoard-xM"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('570bdc2c47270c2a98ca60ff6c5c74cd'
         '64860a464158923815b052e997214867'
         'd41d8cd98f00b204e9800998ecf8427e')

prepare() {
  cd u-boot-${pkgver}
  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset LDFLAGS CFLAGS CXXFLAGS

  make distclean 
  make omap3_beagle_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
