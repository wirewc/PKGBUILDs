# Maintainer: CruX <crux@project-insanity.org>
# Contributor: pepedog at archlinuxarm dot com

buildarch=4
pkgname=libfslvpuwrap
pkgver=3.14.28_1.0.0
_pkgver=1.0.58
pkgrel=1
pkgdesc="Wrapper library for the freescale proprietary VPU extensions"
url="https://community.freescale.com/docs/DOC-95560"
arch=('armv7h')
license=('proprietary')
depends=("imx-vpu=$pkgver")
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${_pkgver}.bin")
md5sums=('bf4960ab18e6bb59e98aae797b80cd7f')

prepare() {
  cd "${srcdir}"
  #extract the sources
  sh ${pkgname}-${_pkgver}.bin --force --auto-accept
}

build() {
  cd "${pkgname}-${_pkgver}"
  export CFLAGS="$CFLAGS -O2 -I/opt/fsl/include"
  export CPPFLAGS="$CPPFLAGS -O2 -I/opt/fsl/include"
  export LDFLAGS="$LDFLAGS -L/opt/fsl/lib"
  ./autogen.sh --prefix=/opt/fsl
  make all
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR=${pkgdir} install

  install -D -m0644 "${srcdir}/${pkgname}-${_pkgver}/COPYING" "$pkgdir/opt/fsl/licenses/LICENSE.${pkgname}"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/fsl/lib" > "${pkgdir}/etc/ld.so.conf.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
