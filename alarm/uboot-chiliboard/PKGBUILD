# U-Boot: Chiliboard
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-chiliboard
pkgver=2014.07
pkgrel=1
pkgdesc="U-Boot for Chiliboard"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
makedepends=('git' 'bc')
license=('GPL')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-Add-linux-compiler-gcc5.h-to-fix-builds-with-gcc5.patch'
        '0002-ARM-asm-io.h-use-static-inline.patch'
        '0003-common-main.c-make-show_boot_progress-__weak.patch'
        '0004-common-board_f-cosmetic-use-__weak-for-leds.patch'
        '0005-chiliboard-support.patch'
        '0006-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('36d4bad687edcafa396fee607e505d4e'
         'e070e8b5cf8628b6440dfc9dbd87cb65'
         '318310de19110167fe15ceac3ae32f7d'
         '1792346431786e1442c4059cd3f288ed'
         '2a32a77f123391c02935754ced2743c2'
         '5951c799b35c526f02edbcb3eb1295fc'
         'be92986c37aaaed20f702e0cda798b26'
         '0afad1f6f3f8609db6e1f5bb322b2a1a')

prepare() {
  cd u-boot-${pkgver}
  git apply ../0001-Add-linux-compiler-gcc5.h-to-fix-builds-with-gcc5.patch
  git apply ../0002-ARM-asm-io.h-use-static-inline.patch
  git apply ../0003-common-main.c-make-show_boot_progress-__weak.patch
  git apply ../0004-common-board_f-cosmetic-use-__weak-for-leds.patch
  git apply ../0005-chiliboard-support.patch
  git apply ../0006-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS CXXFLAGS LDFLAGS

  make distclean 
  make chiliboard_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
