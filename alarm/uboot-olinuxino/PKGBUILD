# U-Boot for i.MX233-OLinuXino
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Kiril Zyapkov <kiril.zyapkov@gmail.com>

buildarch=2

pkgname=uboot-olinuxino
pkgdesc="U-Boot for i.MX233-OLinuXino"
pkgver=2014.07
pkgrel=1
arch=('arm')
license=('GPL')
#makedepends=('elftosb')
install=$pkgname.install
options=('!strip')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'alarm.patch'
        'uEnv.txt')
md5sums=('36d4bad687edcafa396fee607e505d4e'
         'a6a377a08f24dd21156e38e034f6f714'
         '239b6223bbfe21e0cad629723a3abc2d')

prepare() {
  cd "$srcdir/u-boot-${pkgver}"
  patch -Np1 -i ../alarm.patch
}

build() {
  cd "$srcdir/u-boot-${pkgver}"

  unset LDFLAGS CFLAGS CXXFLAGS

  make distclean
  make mx23_olinuxino_config
  make u-boot.sb
}

package() {
  mkdir -p "${pkgdir}/boot"
  cp "${srcdir}/u-boot-${pkgver}"/u-boot.sb "${srcdir}"/uEnv.txt "${pkgdir}/boot"
}
