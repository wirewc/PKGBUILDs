# U-Boot: TrimSlice
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-trimslice
pkgver=2014.10
pkgrel=1
pkgdesc="U-Boot for TrimSlice"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
makedepends=('cbootimage' 'git' 'dtc' 'uboot-tools')
license=('GPL')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "git+https://github.com/NVIDIA/cbootimage-configs.git"
        "boot.txt")
md5sums=('3ddcaee2f05b7c464778112ec83664b5'
         'SKIP'
         'e0fb7b530845c0a6bcf9a7147eed1123')

build() {
  cd u-boot-${pkgver}

  unset LDFLAGS

  make distclean 
  make trimslice_config
  make
}

package() {
  mkdir -p "${pkgdir}"/boot
  mkimage -A arm -O linux -T script -C none -a 0 -e 0 -n "TrimSlice" -d boot.txt "${pkgdir}"/boot/boot.scr
  cp boot.txt "${pkgdir}"/boot

  cd cbootimage-configs/tegra20/compulab/trimslice
  ln -s ${srcdir}/u-boot-${pkgver}/u-boot-dtb-tegra.bin u-boot.bin
  ./build.sh

  cp trimslice-mmc.img trimslice-spi.img "${pkgdir}"/boot
}
