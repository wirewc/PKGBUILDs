# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Huulivoide <gmail.com: jesse.jaara>
# Contributor: Jonathan Hudson <daria.co.uk: jh+arch>

buildarch=16

pkgname=omxplayer-git
pkgver=380.6ee9a0a
pkgrel=1
pkgdesc="omxplayer is a command line media player for the RaspberryPi"
arch=('arm' 'armv6h')
url="https://github.com/popcornmix/omxplayer"
license=(GPL2)
depends=('ffmpeg' 'raspberrypi-firmware-tools' 'fbset')
makedepends=('git' 'boost')
optdepends=('ttf-freefont')
provides=(omxplayer)
conflicts=('omxplayer' 'omxplayer-bin')
source=('git://github.com/popcornmix/omxplayer.git'
        '0001-alarm-fixes.patch'
        '0002-ffmpeg-2.4-fix.patch')
md5sums=('SKIP'
         '825bb619d32a3ba9f8aff81fa329d87e'
         'c80b6c4578dd6a48b7d5bd0da650843e')

pkgver() {
  cd omxplayer
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/omxplayer"

  git apply ../0001-alarm-fixes.patch
  git apply ../0002-ffmpeg-2.4-fix.patch

  sed 's|truetype/freefont|TTF|g' -i omxplayer.cpp
}

build() {
  cd "${srcdir}/omxplayer"

  make
}

package() {
  cd "${srcdir}/omxplayer"

  make DESTDIR="${pkgdir}" install
}
