# U-Boot: Pandaboard and Pandaboard ES
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-pandaboard
pkgver=2015.01
pkgrel=1
pkgdesc="U-Boot for Pandaboard and Pandaboard ES"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
makedepends=('git' 'bc')
license=('GPL')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('7f08dc9e98a71652bd6968888ed6ec95'
         '4ea27f04967623eda45cc468c20db74b'
         '86004660f2912861a040e335c9c0e85b')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset LDFLAGS

  make distclean 
  make omap4_panda_config
  make
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
