# U-Boot: Pandaboard and Pandaboard ES
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-pandaboard
pkgver=2015.04
pkgrel=1
pkgdesc="U-Boot for Pandaboard and Pandaboard ES"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
makedepends=('git' 'bc')
license=('GPL')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('570bdc2c47270c2a98ca60ff6c5c74cd'
         '6a4060a6cef75dd47b841c7a46a866e8'
         '86004660f2912861a040e335c9c0e85b')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset LDFLAGS

  make distclean 
  make omap4_panda_config
  make
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
