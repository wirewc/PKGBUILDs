# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Alexander Werner <bundeswerner@googlemail.com>

pkgname=xinput_calibrator
pkgver=0.7.5.r88.g03dadf5
pkgrel=1
pkgdesc="A generic touchscreen calibration program for X.Org"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/xinput_calibrator"
license=('MIT')
depends=('libx11' 'libxi' 'libxext' 'libxfixes')
makedepends=('git')
source=("git+https://github.com/tias/xinput_calibrator.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd ${pkgname}

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install
  install -m755 scripts/xinput_calibrator{_get_hal_calibration.sh,_pointercal.sh} "${pkgdir}/usr/bin/"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
