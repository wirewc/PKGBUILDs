# U-Boot: Beaglebone and Beaglebone Black
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-beaglebone
pkgver=2015.04
pkgrel=1
pkgdesc="U-Boot for Beaglebone and Beaglebone Black"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
makedepends=('git' 'bc')
license=('GPL')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('570bdc2c47270c2a98ca60ff6c5c74cd'
         'b09551195d8c45ab7c0ec5df8d66e522'
         'ccb79775b81654e16fca18825c7f2506')

prepare() {
  cd u-boot-${pkgver}
  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS CXXFLAGS LDFLAGS

  make distclean 
  make am335x_evm_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
