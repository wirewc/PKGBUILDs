# U-Boot: Beaglebone and Beaglebone Black
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-beaglebone
pkgver=2014.07
pkgrel=2
pkgdesc="U-Boot for Beaglebone and Beaglebone Black"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('bc')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'alarm.patch'
        'uEnv.txt')
md5sums=('36d4bad687edcafa396fee607e505d4e'
         'c0137fc5549efcc1ec3b32c421bea286'
         'ccb79775b81654e16fca18825c7f2506')

prepare() {
  cd u-boot-${pkgver}
  patch -p1 -i ../alarm.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS
  unset CXXFLAGS

  make distclean 
  make am335x_evm_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
