# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - export flags for rpi

# ALARM: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>
#  - build for armv7h

buildarch=4

pkgname=weston-rpi
pkgver=1.8.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor (Raspberry Pi 2)'
arch=('armv7h')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('libxkbcommon' 'libinput' 'libunwind' 'poppler-glib' 'mtdev' 'libxcursor' 'glu' 'pango' 'colord')
makedepends=('raspberrypi-firmware')
provides=('weston')
conflicts=('weston')
source=("http://wayland.freedesktop.org/releases/weston-$pkgver.tar.xz")
sha1sums=('d49e06e8dfbef9bcba8589a20b5bfa2dddc4a7bd')

build() {
	export RPI_BCM_HOST_LIBS="-L/opt/vc/lib"
	export RPI_BCM_HOST_CFLAGS="-I/opt/vc/include -I/opt/vc/include/interface/vcos/pthreads -I/opt/vc/include/interface/vmcs_host/linux"
	export WESTON_NATIVE_BACKEND="rpi-backend.so"
	export RPI_COMPOSITOR_LIBS="-ludev -lmtdev -lbcm_host"
	CONFIG='--disable-egl'

	cd weston-$pkgver
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--enable-demo-clients-install $CONFIG
	make
}

package() {
	cd weston-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
