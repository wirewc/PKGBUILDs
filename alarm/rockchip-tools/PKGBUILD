# Rockchip Tools
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=rockchip-tools
pkgver=20150404
pkgrel=1
pkgdesc="Rockchip Tools"
arch=('armv7h')
url="https://github.com/neo-technologies"
depends=('openssl' 'libusb')
makedepends=('git')
license=('custom' 'APACHE' 'GPL')
source=("git+https://github.com/neo-technologies/rkflashtool.git"
        "git+https://github.com/neo-technologies/rockchip-mkbootimg.git"
        "git+https://github.com/neo-technologies/rkboottools.git")
md5sums=('SKIP'
         'SKIP'
         'SKIP')

build() {
  cd "${srcdir}/rkflashtool"
  sed -i -e '/^CFLAGS/d' -e '/^LDFLAGS/d' Makefile
  make

  cd "${srcdir}/rockchip-mkbootimg"
  sed -i 's/LDFLAGS ?=/LDFLAGS +=/' Makefile
  make

  cd "${srcdir}/rkboottools"
  make
}

package() {
  install -d "${pkgdir}"/usr/bin

  cd "${srcdir}/rkflashtool"
  install -m755 rkcrc rkflashtool rkmisc rkpad rkparameters rkparametersblock rkunpack rkunsign "${pkgdir}"/usr/bin

  cd "${srcdir}/rockchip-mkbootimg"
  install -m755 afptool img_maker mkbootimg mkcpiogz mkrootfs mkupdate unmkbootimg unmkcpiogz "${pkgdir}"/usr/bin

  cd "${srcdir}/rkboottools"
  install -m755 rk-makebootable rk-rc4 rk-splitboot "${pkgdir}"/usr/bin
}
