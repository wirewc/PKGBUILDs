# U-Boot: Boundary Devices Nitrogen6X/Sabre Lite
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-boundary
pkgver=2014.07
pkgrel=1
arch=('armv7h')
pkgdesc="U-Boot for Nitrogen6X/Sabre Lite"
url="https://github.com/boundarydevices/u-boot-imx6/tree/production"
license=('GPL')
makedepends=('git' 'bc')
backup=('6x_bootscript')
_commit=bb9dde563768731423fd6c560e95e1793a90710a
source=("https://github.com/boundarydevices/u-boot-imx6/archive/${_commit}.tar.gz"
        '0001-arch-linux-arm-modifications.patch'
        '0002-kernel-add-support-for-gcc-5.patch'
        '0003-ARM-asm-io.h-use-static-inline.patch'
        '0004-common-main.c-make-show_boot_progress-__weak.patch'
        '0005-common-board-use-__weak.patch'
        '0006-common-board_f-cosmetic-use-__weak-for-leds.patch'
        '6x_bootscript')
md5sums=('b12f5f383c57de06f16625b3465e74d9'
         'cda62610b898b6e0ee13e64882a63107'
         '721a46867e189d8dedc6b6f86a536a34'
         'f6b687eca2d2d01f741cbda90dbacb41'
         '8087672256020417438b12ec4946e1cf'
         'a536d28bf45add6dbf9f84277f943de8'
         '2823d0e0c3c826632f6ae934f2b746d9'
         '8a6b2f50fef47b1ebb011a6d697f0025')

prepare() {
  cd u-boot-imx6-${_commit}

  git apply ../0001-arch-linux-arm-modifications.patch
  git apply ../0002-kernel-add-support-for-gcc-5.patch
  git apply ../0003-ARM-asm-io.h-use-static-inline.patch
  git apply ../0004-common-main.c-make-show_boot_progress-__weak.patch
  git apply ../0005-common-board-use-__weak.patch
  git apply ../0006-common-board_f-cosmetic-use-__weak-for-leds.patch
}

build() {
  cd u-boot-imx6-${_commit}
  
  unset CFLAGS
  unset CXXFLAGS
  
  make distclean
  make nitrogen6q_config
  make
}

package() {
  cd u-boot-imx6-${_commit}

  mkdir -p "${pkgdir}"/boot
  cp u-boot.imx "${pkgdir}"/boot
  tools/mkimage -A arm -O linux -T script -C none -a 0 -e 0 -n "boot script" -d ../6x_bootscript ${pkgdir}/6x_bootscript
}
