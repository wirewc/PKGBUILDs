# U-Boot: Wandboard
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-wandboard
pkgname=('uboot-wandboard-solo' 'uboot-wandboard-dual' 'uboot-wandboard-quad')
pkgver=2014.10
pkgrel=1
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git' 'bc')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        '0001-arch-linux-arm-modifications.patch')
md5sums=('3ddcaee2f05b7c464778112ec83664b5'
         'edae26c4acf325b590f8b40a0209c62e')

prepare() {
  cd u-boot-${pkgver}
  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}
  
  unset LDFLAGS
  
  mkdir bins
  for i in solo dl quad; do
    make distclean
    make wandboard_${i}_config
    make
    mv u-boot.imx bins/u-boot-${i}.imx
  done
}

package_uboot-wandboard-solo() {
  pkgdesc="U-Boot for Wandboard Solo"
  conflicts=('uboot-wandboard-dual' 'uboot-wandboard-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/bins/u-boot-solo.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-wandboard-dual() {
  pkgdesc="U-Boot for Wandboard Dual"
  conflicts=('uboot-wandboard-solo' 'uboot-wandboard-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/bins/u-boot-dl.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-wandboard-quad() {
  pkgdesc="U-Boot for Wandboard Quad"
  conflicts=('uboot-wandboard-solo' 'uboot-wandboard-dual')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/bins/u-boot-quad.imx "${pkgdir}"/boot/u-boot.imx
}
