# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=ec-utils
pkgver=r5005.cef3984
pkgrel=1
pkgdesc="Chromium OS Embedded Controller utilities"
arch=('armv7h')
makedepends=('git' 'inetutils')
license=('custom:chromiumos')
source=("git+https://chromium.googlesource.com/chromiumos/platform/ec")
md5sums=('SKIP')

pkgver() {
  cd ec
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ec
  sed -i 's/ccache //g' Makefile.toolchain
  cd util
  sed -i 's/_BSD_SOURCE/_DEFAULT_SOURCE/g' stm32mon.c ec_uartd.c
}

build() {
  cd ec
  HOST_CROSS_COMPILE= make utils-host
}

package() {
  cd ec
  install -dm755 ${pkgdir}/usr/bin
  install -m755 build/bds/util/{ectool,lbplay,stm32mon,ec_sb_firmware_update,lbcc} ${pkgdir}/usr/bin
}
