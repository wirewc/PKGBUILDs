# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for libwebp aarch64 neon decision

pkgname=qt5-imageformats
_qtver=5.6.0
pkgver=${_qtver/-/}
pkgrel=1
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
pkgdesc='Plugins for additional image formats: TIFF, MNG, TGA, WBMP'
depends=('qt5-base' 'libtiff' 'jasper')
makedepends=()
conflicts=('qt')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-opensource-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        'aarch64.patch')
md5sums=('bb57c1db37e89a47f3513859ef04ae97'
         'c467ac74c0faeac0fab836181e228e1d')

prepare() {
  mkdir -p build

  cd ${_pkgfqn}
  patch -p1 -i ../aarch64.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
