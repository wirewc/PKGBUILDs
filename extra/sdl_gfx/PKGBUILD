# $Id: PKGBUILD 144350 2011-12-04 22:59:47Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - need --disable-mmx for ARM as well

pkgname=sdl_gfx
pkgver=2.0.25
pkgrel=1
pkgdesc="SDL Graphic Primitives"
arch=('i686' 'x86_64')
license=('LGPL')
options=()
url="http://www.ferzkopp.net/joomla/software-mainmenu-14/4-ferzkopps-linux-software/19-sdlgfx"
source=(http://www.ferzkopp.net/Software/SDL_gfx-2.0/SDL_gfx-${pkgver}.tar.gz)
depends=('sdl')
md5sums=('ea24ed4b82ff1304809c363494fa8e16')

build() {
  cd SDL_gfx-${pkgver}

  if [ "${CARCH}" == "x86_64" -o "${CARCH}" == "arm" -o "${CARCH}" == "armv6h" -o "${CARCH}" == "armv7h" -o "${CARCH}" == "aarch64" ]; then
    ./configure --prefix=/usr --disable-mmx
  else
    ./configure --prefix=/usr
  fi

  make
}

package() {
  cd SDL_gfx-${pkgver}
  make DESTDIR=${pkgdir} install
}
