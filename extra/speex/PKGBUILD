# $Id: PKGBUILD 137583 2011-09-09 22:58:20Z allan $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jason Chu <jason@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enhancements for armv5

pkgname=speex
pkgver=1.2rc2
pkgrel=1
pkgdesc="A free codec for free speech"
arch=('i686' 'x86_64')
url="http://www.speex.org/"
license=('BSD')
depends=(libogg speexdsp)
options=('!libtool')
source=(http://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('6ae7db3bab01e1d4b86bacfa8ca33e81')

build() {
  cd $srcdir/speex-$pkgver

  [[ "$CARCH" == "arm" ]] && CONFIG="--enable-arm5e-asm --enable-fixed-point"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static $CONFIG \
    --enable-binaries # Must be given or configure won't use pkg-config correctly
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
