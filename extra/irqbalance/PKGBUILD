# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed dependency on numactl
#  - updated source to new location
#  - run autogen.sh

pkgname=irqbalance
pkgver=1.0.8
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('i686' 'x86_64')
url="http://code.google.com/p/irqbalance"
license=('GPL')
depends=(glib2 libcap-ng)
makedepends=(pkgconfig)
source=("https://github.com/Irqbalance/irqbalance/archive/v${pkgver}.tar.gz"
        'irqbalance.service')
md5sums=('2a7090abab6bba15d632c24f5554e7ae'
         'a97ad17dc7df98b81acb500b27f931e5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  install -D -m644 ../irqbalance.service "$pkgdir"/usr/lib/systemd/system/irqbalance.service
}
