# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable omap, exynos, freedreno for v7
#  - drop valgrind makdepend for v5 and v6
#  - upstream fix to valgrind detection

pkgname=libdrm
pkgver=2.4.62
pkgrel=1.1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('xorg-util-macros')
makedepends_armv7h=('valgrind')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig}
        0001-configure-default-enable-valgrind-to-auto.patch
        COPYING)
sha256sums=('906c294bdbe1c94c3ca084305d61a6e5a8367f3b4986e6cc13b1e9b3f75931dc'
            'SKIP'
            '3fc9bb4279486e3a4cb0d86d50b6999f95d0606e83f1f804b3f110d371b74588'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')
validpgpkeys=('B97BD6A80CAC4981091AE547FE558C72A67013C3') # Maarten Lankhorst <maarten.lankhorst@canonical.com>
validpgpkeys+=('215DEE688925CCB965BE5DA97C03D7797B6E1AE2') # Damien Lespiau <damien.lespiau@intel.com>
validpgpkeys+=('10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE') # David Airlie <airlied@redhat.com>
validpgpkeys+=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>

prepare() {
  cd $pkgname-$pkgver

  # upstream fix
  patch -p1 -i ../0001-configure-default-enable-valgrind-to-auto.patch

  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install

}

build() {
  cd $pkgname-$pkgver  
  [[ "$CARCH" == "armv7h" ]] && CONFIG="--enable-omap-experimental-api --enable-exynos-experimental-api --enable-tegra-experimental-api"
  ./configure --prefix=/usr --enable-udev $CONFIG
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
