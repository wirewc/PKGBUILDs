# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

# remove when bumped upstream

pkgname=spirv-tools
pkgver=1.0.3
_pkgver=1.0-rev3
pkgrel=1
pkgdesc="API and commands for processing SPIR-V modules"
arch=('i686' 'x86_64')
url="https://www.khronos.org/vulkan/"
license=('custom')
groups=(vulkan-devel)
depends=(gcc-libs)
makedepends=(cmake)
source=("https://github.com/KhronosGroup/SPIRV-Tools/archive/spirv-${_pkgver}.tar.gz")
sha256sums=('7a305ec31a9bd0fd0d8bae57550e57c531f1758e8edbea7e8ea1c46a1329a8c1')

build() {
  cd SPIRV-Tools-spirv-"${_pkgver}"
  
  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../
  make
}

package() {
  cd SPIRV-Tools-spirv-"${_pkgver}"/build
  
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
