# $Id$
# Maintainer: Steven Allen <steven@stebalien.com>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Amr Hassan <amr.hassan@gmail.com>

pkgname=python-pylast
pkgver=1.0.0
pkgrel=1
pkgdesc='A Python interface to the last.fm API'
arch=('any')
url='https://github.com/pylast/pylast'
license=('Apache')
depends=('python')
source=("http://pypi.python.org/packages/source/p/${pkgname#*-}/${pkgname#*-}-$pkgver.tar.gz")
sha256sums=('948bc9efb2e2785db8c303cc6559358b943538cc45f902838db770b84843d605')

build() {
  cd ${pkgname#*-}-$pkgver

  python setup.py build
}

package() {
  cd ${pkgname#*-}-$pkgver

  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:
